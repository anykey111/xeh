
: doc-for-core "Core" with-tag ;
: doc-for-loop "Conditional Loops" "section" with-tag ;
: doc-for-stack "Stack Manipulation" "section" with-tag ;
: doc-for-cond  "Conditional Execution" "section" with-tag ;
: doc-for-tags "Tags" "section" with-tag ;
: doc-for-bitstr "Binary Parsing" with-tag ;
: doc-for-fmt "Formatting" with-tag ;
: doc-for-doc "Documentation" with-tag ;
: doc-for-bitwise "Bitwise Arithmetic" with-tag ;
: doc-for-d2 "2D Canvas" with-tag ;
: stack-comment "stack-comment" with-tag ;
: doc-example "example" with-tag ;

"Add help string for word."
.[ doc-for-doc "str str --" stack-comment
    "\"Help string for \"fun1\" \"fun1\" doc!" doc-example
] "doc!" doc!

"Print help string for word."
.[ doc-for-doc "str --" stack-comment
    "\"fun1\" help" doc-example
] "help" doc!

"Get string for word."
.[ doc-for-doc "str -- str" stack-comment
"\"fun1\" help-str println" doc-example
] "help-str" doc!

"Stick tag to the val. If value already has a tag drop the old one."
.[ doc-for-tags
    "val tag -- val" stack-comment 
] "with-tag" doc!

"Read tag of the value or nil if tag is absent."
.[ doc-for-tags
    "val -- tag" stack-comment
] "tag-of" doc!

"Insert tagged pair to the list of tags or replace if key already exists."
.[ doc-for-tags
    "val tag-val tag-key -- val" stack-comment
    "# add text color tag
    \"sample text\"
        0xffeedd \"color\" set-tagged
        true \"bold\" set-tagged
    # result \"sample text\" .[ 0xffeedd . \"color\" true . \"bold\" ]"
    doc-example 
] "set-tagged" doc!

"Get tagged value from the list of tags."
.[ doc-for-tags
    "val tag-key -- tag-val" stack-comment
    "100 .[ \"u8\" . \"type\" 8 . \"size\" ]
    \"size\" get-tagged # result 8" doc-example
] "get-tagged" doc!

"Execute code if flag is true, otherwise jump to \"endif\".
    FLAG if CODE endif"
.[ doc-for-cond
] "if" doc!

"Fallback for the \"if\" conditional branch. Executed when flag is false.
    FLAG if CODE else FALLBACK endif"
.[ doc-for-cond
] "else" doc!

"End of the \"if\" conditional branch."
.[ doc-for-cond
] "endif" doc!

"Select one case of the multiple different choices.
    X case
        X1 of CODE1 endof
        XN of CODEN endof
        DEFAULT
    endcase
Compare X with X1, if X equals to X1 then CODE1 is executed.
Compare X with XN, if X equals to XN then CODEN is executed.
Otherwise DEFAULT is executed."
.[ doc-for-cond
] dup "of" doc! "case" doc!

"End of the \"case\" conditional select." 
.[ doc-for-cond
] "endcase" doc!

"End of the single case selection." 
.[ doc-for-cond
] "endof" doc!

"Loop with pre-condition.
    begin FLAG while CODE repeat
If FLAG is true CODE is executed and loop is restarted from \"begin\".
IF FLAG is false execution continue after the \"repeat\".
The FLAG is tested on every loop iteration.
Endless loop
    begin CODE repeat
"
.[ doc-for-loop 
] dup "begin" doc! dup "while" doc! "repeat" doc!

"Loop with post-condition.
    begin CODE FLAG until
Restart loop until flag is false."
.[ doc-for-loop
] "until" doc!

"Leave the innermost loop immediatly."
.[ doc-for-loop
] "leave" doc!

"Loop for every integer starting from START and up to, but excluding LIMIT.
    LIMIT START do CODE loop
The current loop index can be accessed with I.
J is used to access the outer loop index and the outermost loop index is K."
.[ doc-for-loop
"10 0 do \".\" println loop" doc-example
"5 var num-rows
10 var num-columns
num-rows 0 do
    num-columns do
        \"row=\" print J print
        \"col=\" print I println
    loop
loop" doc-example
] dup "do" doc!  "loop" doc!

"Innermost loop index"
.[ doc-for-loop "-- int" stack-comment ] "I" doc!

"Outer loop index"
.[ doc-for-loop "-- int" stack-comment ] "J" doc!

"Outermost loop index"
.[ doc-for-loop "-- int" stack-comment ] "K" doc!

"Binary data under inspection."
.[ doc-for-bitstr 
    "-- bitstr" stack-comment
] "input" doc!

"Number of bits consumed."
.[ doc-for-bitstr
    "-- int" stack-comment
] "offset" doc!

"Number of bits remain unread."
.[ doc-for-bitstr
    "-- int" stack-comment
] "remain" doc!

"Temporary open the new binary until the close-input called."
.[ doc-for-bitstr 
    "bitstr --" stack-comment
] "open-input" doc!

"Drop current binary, restore the previous one from oh-hold."
.[ doc-for-bitstr ] "close-input" doc!

"Reposition offset."
.[ doc-for-bitstr
    "int --" stack-comment
] "seek" doc!

"Convert size in bytes to the number of bits."
.[ doc-for-bitstr
    "int -- int" stack-comment
] "in-bytes" doc!

"Parse base 16 string, ignore whitespaces."
.[ doc-for-bitstr
    "str -- bitstr" stack-comment
    "\"ffff11\" hex>bitstr" doc-example
    "\"ff FF 11\" hex>bitstr" doc-example
] "hex>bitstr" doc!

"Parse base 2 string, ignore whitespaces."
.[ doc-for-bitstr
    "str -- bitstr" stack-comment
    "\"1001 0001\" bin>bitstr" doc-example
] "bin>bitstr" doc!

"Decode UTF-8 string from bitstr."
.[ doc-for-bitstr
    "bitstr -- str" stack-comment
    "|C3 9F| bitstr>utf8" doc-example
] "bitstr>utf8" doc!

"Pack string or vector into bitstr.
Vector can contains integers in range of 0 to 255 representing
single byte, another vector, string or bitstr."
.[ doc-for-bitstr
    "val -- bitstr" stack-comment
    "# pack string
    \"Hello\" >bitstr
    # pack string terminating with zero
    [ \"Hello\" 0 ] >bitstr
    # pack multiple values
    [ -1 i32! |FF CC| [ \"abc\" 0 ] ] >bitstr" doc-example
] ">bitstr" doc!

"True if default endianess is set to big."
.[ doc-for-bitstr
    "flag --" stack-comment
] "big?" doc!

"Set big endian as default."
.[ doc-for-bitstr ] "big" doc!

"Set little endian as default."
.[ doc-for-bitstr ] "little" doc!
 
"Find the first occurence or nil starting from the current offset."
.[ doc-for-bitstr
    "bytestr -- int" stack-comment
] "find" doc!

"Print dump from the current offset."
.[ doc-for-bitstr
    "--" stack-comment
] "dump" doc!

"Print dump at the given offset."
.[ doc-for-bitstr
    "int --" stack-comment
] "dump-at" doc!

"Read number of bits as bitstr."
.[ doc-for-bitstr
    "int -- bitstr" stack-comment
] "bits" doc!

"Read given number of bytes."
.[ doc-for-bitstr
    "int -- bitstr" stack-comment
] "bytes" doc!

"Read null-terminated bytestr, including 0x00 byte."
.[ doc-for-bitstr
    "-- bitstr" stack-comment
] "nulbytestr" doc!

"Read null-terminated ascii string, also known as C string."
.[ doc-for-bitstr
    "-- str" stack-comment
] "cstr" doc!

"Read variable length signed integer."
.[ doc-for-bitstr 
    "int -- int" stack-comment
] "int" doc!

"Read variable length unsigned integer."
.[ doc-for-bitstr 
    "int -- int" stack-comment
] "uint" doc!

"Pack variable length signed integer."
.[ doc-for-bitstr 
    "int int -- bitstr" stack-comment
    "0xff 8 int! println" doc-example
] "int!" doc!

"Pack variable length unsigned integer."
.[ doc-for-bitstr 
    "int int -- bitstr" stack-comment
    "0xff 8 uint! println" doc-example
] "uint!" doc!

"Read variable length float."
.[ doc-for-bitstr 
    "int -- float" stack-comment
] "float" doc!

"Pack variable length float."
.[ doc-for-bitstr
    "int float -- bitstr" stack-comment
    "0.111 32 float! println" doc-example
] "float!" doc!

"Read bitstr of exact prefix or raise error."
.[ doc-for-bitstr
    "bitstr -- bitstr" stack-comment
    "# test PNG file header
    |89 50 4e 47 0d 0a 1a 0a| magic
    # test NES ROM image header
    [ \"NES\" 0x1a ] >bitstr magic" doc-example
] "magic" doc!

"Write bitstr to output buffer."
.[ doc-for-bitstr
    "bitstr --" stack-comment
    "|31323500| emit" doc-example
 ] "emit" doc!

"Number of bits emited to output."
.[ doc-for-bitstr
    "-- int" stack-comment
] "output-length" doc!

"Bitstr output buffer. "
.[ doc-for-bitstr
] "output" doc!

: generate-int-docs
    local n
    [ "Read " n " bits signed integer" ] concat
    .[ doc-for-bitstr "-- bitstr" stack-comment ] [ "i" n ] concat doc!
    [ "Pack " n " bits signed integer" ] concat
    .[ doc-for-bitstr "int -- bitstr" stack-comment ] [ "i" n "!" ] concat doc!
    [ "Read " n " bits unsigned integer" ] concat
    .[ doc-for-bitstr "-- bitstr" stack-comment ] [ "u" n ] concat doc!
    [ "Pack " n " bits unsigned integer" ] concat
    .[ doc-for-bitstr "int -- bitstr" stack-comment ] [ "u" n "!" ] concat doc!
;

: generate-uint-list
    [ 8 16 32 64 ] local nbits
    nbits length 0 do
        nbits I get generate-int-docs
    loop
;

generate-uint-list

"Read 32 bits float"
.[ doc-for-bitstr "-- real" stack-comment ] "f32" doc!

"Read 64 bits float"
.[ doc-for-bitstr "-- real" stack-comment ] "f64" doc!

"Pack 32 bit float"
.[ doc-for-bitstr "real -- bitstr" stack-comment ] "f32!" doc!

"Pack 64 bit float"
.[ doc-for-bitstr "real -- bitstr" stack-comment ] "f64!" doc!

"Formatting option, set the number base to 16."
.[ doc-for-fmt
"13 HEX println" doc-example
] "HEX" doc!

"Formatting option, set the number base to 2."
.[ doc-for-fmt
"5 BIN println" doc-example
] "BIN" doc!

"Formatting option, set the number base to 10."
.[ doc-for-fmt
"13 HEX print 13 DEC println" doc-example
] "DEC" doc!

"Formatting option, set the number base to 8."
.[ doc-for-fmt
"13 OCT println" doc-example
] "OCT" doc!

"Formatting option, display the number prefix."
.[ doc-for-fmt
] "PREFIX" doc!

"Formatting option, omit the number prefix."
.[ doc-for-fmt
] "NO-PREFIX" doc!

"Formatting option, display the tag sticked to the value."
.[ doc-for-fmt ] "TAGS" doc!

"Formatting option, omit the tag sticked to the value."
.[ doc-for-fmt ] "NO-TAGS" doc!

"Formatting otion, display in upper case."
.[ doc-for-fmt ] "UPCASE" doc!

"Formatting otion, display in lower case."
.[ doc-for-fmt ] "LOCASE" doc!

"Decompile the definition."
.[  doc-for-core "<name> --" stack-comment
] "see" doc!

"Length of the vector,string or bitstr."
.[ doc-for-core "vec -- int" stack-comment
    "[ 1 2 3 ] length
    \"hello\" length
    # please, note, bitstr length is always in number of bits
    |fff| length" doc-example
] "length" doc!

"Get vector item at index.
Negative index access items from the tail of the vector."
.[ doc-for-core "vec int -- val" stack-comment
    "[ 5 7 ] 0 get
    # get last item of the vec
    [ 5 7 ] -1 get" doc-example
] "get" doc!

"Concatenate vector of values into a string."
.[ doc-for-core "vec -- str" stack-comment
    "[ \"a\" 2 2 + \"b\" ] concat
    # result \"a3b\"" doc-example
] "concat" doc!

"Join vector into string using separator."
.[ doc-for-core "vec str - str" stack-comment
    "[ 1 2 ] \"+\" join
    # result \"1+2\" " doc-example
] "join" doc!

"Collect top N stack items into the vector."
.[ doc-for-core "int -- vec" stack-comment
    "\"a\" \"b\" 2 collect
    # result [ \"a\" \"b\" ]" doc-example
] "collect" doc!

"Unbox vector items into the stack."
.[ doc-for-core "vec -- val ..." stack-comment
    "[ 1 2 3 ] unbox
    # result 3 2 1" doc-example
] "unbox" doc!

"Append item to the back of a vector."
.[ doc-for-core "val vec -- vec" stack-comment
    "1 [ 3 ] push
    # result [ 3 1 ]" doc-example
] "push" doc!

"Include source file at build time."
.[ doc-for-core
    "include \"src/test-hello.xeh\"" doc-example
] "include" doc!

"Include source file at build time only if it's not included already."
.[ doc-for-core
    "require \"foo-library.xeh\"" doc-example
] "require" doc!

"Test that dictionary contains word."
.[ doc-for-core
    "defined DEBUG if 
        require \"debug-library.xeh\" 
    else
        require \"release-library.xeh\"
    endif" doc-example
] "defined" doc!

"Create a new file or truncate existing, then write bitstr with padding."
.[ doc-for-bitstr 
    "str bitstr --" stack-comment
    "\"test.bin\" big 777 u32! write-all" doc-example
] "write-all" doc!

"Read all bytes from file."
.[ doc-for-bitstr
    "str -- bitstr" stack-comment
    "\"test-file.txt\" read-all" doc-example
] "read-all" doc!

"Define global variable with initial value."
.[ doc-for-core 
    "1 var ONE" doc-example
] "var" doc!

"Define local variable with initial value."
.[ doc-for-core
": f
    local a # take value from top of the stack 
    1 local b # assign the new value
    a b + ; 
3 f println" doc-example
] "local" doc!

"Bind names to the values inside of a data structure.
Raise error if structure didn't match with user definition.
Optional `else` path is taken in case of error.
    let <name-or-literal> in
    let <name-or-literal> else CODE in"
.[ doc-for-core
    "1 let a in
    [ 1 2 ] let [ c d ]
    a let 2 else \"invalid value\" println in"  doc-example
] dup "let" doc!
"in" doc!

"Duplicate top of the stack."
.[ doc-for-core
    "x -- x x" stack-comment
    "1 dup" doc-example
] "dup" doc!

"Drop top of the stack."
.[ doc-for-core
    "x --" stack-comment
] "drop" doc!

"Exchange top stack item with the second one."
.[ doc-for-core
    "x1 x2 -- x2 x1" stack-comment
    "1 5 swap" doc-example
] "swap" doc!

"Exchange top stack item with the third one."
.[ doc-for-core
    "x1 x2 x3 -- x3 x2 x1" stack-comment
] "rot" doc!

"Duplicate second top item."
.[ doc-for-core
    "x1 x2 -- x1 x2 x1" stack-comment
] "over" doc!

"Define a word by name without defining its behaviour.
Deffer word resolution to runtime."
.[  doc-for-core "<name>" stack-comment
    "# deffered definition
     defer B 
     : A B ;
     # actual definition
     : B 1 ;
     A println" doc-example
] "defer" doc!

"Convert string to number using default base."
.[ doc-for-core "str -- num" stack-comment
    "HEX \"ff\" str>number" doc-example
] "str>number" doc!

"Split string into head and tail at given index."
.[ doc-for-core "str int -- str str" stack-comment
    "# split at index 1
    \"hello\" 1 str-split # result \"h\" \"ello\"
    # split from the tail
    \"hello\" -1 str-split # result \"hell\" \"o\"
    "
] "str-split" doc!

"Bit-shift left."
.[ doc-for-bitwise
    "int nbits -- int" stack-comment
] "bit-shl" doc!

"Bit-shift right."
.[ doc-for-bitwise
    "int nbits -- int" stack-comment
] "bit-shr" doc!

"Bitwise AND."
.[ doc-for-bitwise
    "int int -- int" stack-comment
] "bit-and" doc!

"Bitwise OR."
.[ doc-for-bitwise
    "int int -- int" stack-comment
] "bit-or" doc!

"Bitwise XOR."
.[ doc-for-bitwise
    "int int -- int" stack-comment
] "bit-xor" doc!

"Bitwise NOT, invert bits."
.[ doc-for-bitwise
    "int -- int" stack-comment
] "bit-not" doc!

"Resize canvas."
.[ doc-for-d2
    "width height --" stack-comment
] "d2-resize" doc!

"Set RGBA color lookup table."
.[ doc-for-d2
    "vec --" stack-comment
    "[ 0x112233ff 0x445566ff ] d2-palette!" doc-example
] "d2-palette!" doc!

"Set default RGBA color or lookup index when d2-palette! is set."
.[ doc-for-d2
    "rgba --" stack-comment
] "d2-color!" doc!

"Draw pixel at position with default color."
.[ doc-for-d2
    "x y --" stack-comment
] "d2-data!" doc!

depth 0 = assert